%MACRO READ_TRIPS(DSNAME, LIB, DIR, SWEEK, EWEEK);

  /* NOTE: FOR THIS TO WORK OVER MOUNTED DRIVES YOU MUST SPECIFY:
      OPTIONS FILELOCKS=NONE */
  %DO WEEK = &SWEEK %TO &EWEEK;
    
    FILENAME DIRLIST PIPE "ls &DIR/trips&WEEK";
    
    DATA DIRLIST_W&WEEK;
      LENGTH FNAME $256; 
      INFILE DIRLIST LENGTH=RECLEN ;
      INPUT FNAME $VARYING256. RECLEN ;
      FILEPATH = "&DIR/trips&WEEK/"||FNAME;
    RUN;
    
  %END;

  DATA DIRLIST;
    SET
      %DO WEEK = &SWEEK %TO &EWEEK;
        DIRLIST_W&WEEK
      %END;
    ;
  RUN;
  
  PROC PRINT DATA = DIRLIST(KEEP = FILEPATH);
    TITLE "FILES READ TO &LIB..&DSNAME";
  RUN;
  
  DATA &LIB..&DSNAME (DROP=FNAME FILEPATH);
    SET DIRLIST;
    INFILE DUMMY FILEVAR = FILEPATH LENGTH=RECLEN END=DONE DSD DLM = "|" MISSOVER;
    DO WHILE(NOT DONE);
      INPUT 
        PANELIST
        STORE
        WEEK
        WEEK_OFFSET
        MINUTE
        DATE1
        CENTS998
        CENTS999
        CENTS;
      OUTPUT;
    END;
  RUN;

%MEND READ_TRIPS;


